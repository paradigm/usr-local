#!/usr/bin/awk -f

BEGIN {
	# seconds since epoch for current date/time
	get_current_datetime="date +%s"
	get_current_datetime | getline current_datetime;
	close(get_current_datetime)

	# get number of characters available for output
	get_width_px="xrandr | awk -F'x| +' '/*/{print$2}'"
	get_width_px | getline width_px;
	close(get_width_px)
	# current preferred font has characters as six pixels wide
	width_chars =  width_px / 6
	# 1-9/mode is 32 px, bat/date/etc is another 32, 10 for window title
	available_width = width_chars - 32 - 32 - 10
}

# setup indexes for new section
/^\[/{
	sec_num++
	item_num = 0
}

# get current line
! /^\[/ && ! /^#/ && ! /^$/ {
	# get item month and day
	item_month = substr($0,6,2)
	item_day   = substr($0,9,2)
	item_msg   = substr($0,12)

	# get number of days until item
	get_item_datetime="date --date=\""$1"\" +%s"
	get_item_datetime | getline item_datetime;
	close(get_item_datetime)
	delta_days = (item_datetime - current_datetime) / (60 * 60 * 24)
	if (delta_days > int(delta_days)) {
		delta_days = int(delta_days) + 1
	} else {
		delta_days = int(delta_days)
	}

	# get day of week
	get_day_of_week="date --date="$1" +%w"
	get_day_of_week | getline day_of_week
	close(get_day_of_week)
	if(day_of_week == 0)
		day_of_week = "S"
	else if(day_of_week == 1)
		day_of_week = "M"
	else if(day_of_week == 2)
		day_of_week = "T"
	else if(day_of_week == 3)
		day_of_week = "W"
	else if(day_of_week == 4)
		day_of_week = "R"
	else if(day_of_week == 5)
		day_of_week = "F"
	else if(day_of_week == 6)
		day_of_week = "A"

	# increment index
	item_num++
	if (item_num > item_max) {
		item_max = item_num
	}
	# add to list
	items[item_num","sec_num] = item_msg" "delta_days""day_of_week""item_month""item_day
}

END {
	# iterate over items, printing the first item in each section, then the
	# second, then the third, etc, until we've exhausted available_width
	output=""
	for (item = 1; item <= item_max; item++) {
		for (sec  =1; sec <= sec_num; sec++) {
			if (item","sec in items) {
				new_output = output""items[item","sec]" "
				if (length(new_output) > available_width) {
					break
				} else {
					output = new_output
				}
			}
		}
	}
	# print output
	printf "%s", output
}
